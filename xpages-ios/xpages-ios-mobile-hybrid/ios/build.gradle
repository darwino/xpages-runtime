buildscript {
    repositories {
        jcenter()
        mavenLocal()
    }
    dependencies {
        // Multi-OS Engine plugin
        classpath group: 'org.multi-os-engine', name: 'moe-gradle', version: '1.4.+'
    }
}

// Apply plugins
apply plugin: 'eclipse'
apply plugin: 'moe'

// Set source and target to Java 8
sourceCompatibility = "1.8"
targetCompatibility = "1.8"

// Set maven repository
repositories {
    jcenter()
}

// Exclude all files from Gradle's test runner
test { exclude '**' }

// Setup Multi-OS Engine
moe {
    xcode {
        project 'xcode/ios.xcodeproj'
        mainTarget 'ios'
        testTarget 'ios-Test'

        // Uncomment and change these settings if you are working with a Xcode workspace
        // workspace 'xcode/ios.xcworkspace'
        // mainScheme 'ios'
        // testScheme 'ios-Test'
    }
    //proguardLevel 'all'
}
moeMainProGuard {
    doLast {
        def jar = getOutJar()
        def outputDir = file(jar.getAbsolutePath() + "_")
        copy{
            from(zipTree(jar)){
                exclude "META-INF/*.SF"
                exclude "META-INF/*.DSA"
                exclude "META-INF/*.RSA"
            }
            into outputDir
        }
        delete jar
        ant.jar(update: "true", index: "true", destfile: jar.getAbsolutePath()) {
            fileset(dir: outputDir)
        }
        delete outputDir
    }
}

configurations {
    nativeDeps
}
dependencies {
    // https://openflow.stanford.edu/display/Beacon/Beacon+on+Android/index.html#BeacononAndroid-Java.Beans for beans.jar
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "com.darwino:dwo-ios-moe-platform-hybrid:${DARWINO_VERSION}"

    implementation "com.darwino:dwo-web-darwino:${DARWINO_VERSION}"

    def ignoreServlet = {
        exclude group: 'com.ibm.xsp', module: 'com.ibm.pvc.servlet'
        exclude group: 'com.ibm.xsp', module: 'com.ibm.notes.java.api.win32.linux'
        exclude group: 'com.ibm.xsp', module: 'com.ibm.xsp.domino'
    }
    def ignoreXmlApis = {
        exclude group: 'xml-apis', module: 'xml-apis'
    }

    implementation "com.darwino.xsp.ios:xpages-ios-shared:${PROJECT_VERSION}", ignoreServlet
    implementation "com.darwino.xsp.ios:xpages-ios-mobile:${PROJECT_VERSION}", ignoreServlet
    implementation "com.darwino.xsp.ios:xpages-ios-webui:${PROJECT_VERSION}", ignoreServlet
    implementation "com.darwino:dwo-commons-runtime-debug:${DARWINO_VERSION}", ignoreServlet
    implementation "com.darwino:dwo-mobile-platform-hybrid-jetty:${DARWINO_VERSION}", ignoreServlet
    implementation 'javax.servlet:jsp-api:2.0'

    // Fill in gaps in the MOE JRE
    implementation 'xerces:xercesImpl:2.11.0', ignoreXmlApis
    implementation 'org.glassfish.corba:glassfish-corba-omgapi:4.2.0'
    implementation 'org.osgi:org.osgi.service.event:1.3.1'
    implementation 'com.ibm.xsp:com.ibm.domino.services:[9.0.1,)', ignoreServlet
    implementation 'javax.xml.stream:stax-api:1.0-2', ignoreXmlApis

    nativeDeps (
        [ group: 'com.darwino', name: 'dwo-native-sqlite', version: "${DARWINO_VERSION}", ext:'a', classifier: 'ios-universal' ]
    )
}
task copyNativeLibraries {
    description 'Copy the native SQLite iOS libraries'
    copy {
        from configurations.nativeDeps[0]
        into 'libs'
        rename {'libJsonstoreUniversal.a'}
    }
}

// Setup Eclipse
eclipse {
    // Set Multi-OS Engine nature
    project {
        natures 'org.multi-os-engine.project'
    }
}
